buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
compileJava {
    sourceCompatibility = '8'
    targetCompatibility = '1.8'
}

archivesBaseName = 'RewardClaim'
version = '0.1'
group = 'me.dancedog.rewardclaim'
minecraft {
    version = '1.8.9-11.15.1.1722'
    runDir = 'run'
    mappings = 'stable_20'
}

configurations {
    shade
    shade.transitive = false
    compile.extendsFrom(shade)
}
repositories {
    mavenCentral()
}
dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    shade 'org.jsoup:jsoup:1.13.1'
}
jar {
    from configurations.shade.collect {
        it.isDirectory() ? it : zipTree(it)
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//noinspection GrDeprecatedAPIUsage
sourceSets.main.output.classesDir = new File(buildDir, "classes/main")